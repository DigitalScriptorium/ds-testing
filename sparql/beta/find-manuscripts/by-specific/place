# prefixes
PREFIX wd: <https://catalog.digital-scriptorium.org/entity/>
PREFIX wds: <https://catalog.digital-scriptorium.org/entity/statement/>
PREFIX wdv: <https://catalog.digital-scriptorium.org/value/>
PREFIX wdt: <https://catalog.digital-scriptorium.org/prop/direct/>
PREFIX p: <https://catalog.digital-scriptorium.org/prop/>
PREFIX ps: <https://catalog.digital-scriptorium.org/prop/statement/>
PREFIX pq: <https://catalog.digital-scriptorium.org/prop/qualifier/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# find manuscript records for manuscripts produced in particular place(s)
SELECT ?record ?recordLabel ?placeString (GROUP_CONCAT(?placeLabel;separator="|") as ?places)
WHERE {
# set variable value for place or places to be queried
VALUES ?place { wd:Q3086 wd:Q3093 }
# bind query variables
  BIND(p:P27 AS ?placeAsRecordedStatement).
  BIND(ps:P27 AS ?placeAsRecorded).
  BIND(pq:P28 AS ?hasPlace).

# statement: manuscript record has statement for place of production
  ?record ?placeAsRecordedStatement ?placeStatement .
# statement: place statement has place object recorded as string value
  ?placeStatement ?placeAsRecorded ?placeString .
# statement: place statement has qualifier for structured/authority value
  ?placeStatement ?hasPlace ?place .

# return English language labels
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en".
    ?record rdfs:label ?recordLabel .
    ?place rdfs:label ?placeLabel .
  }
  }
GROUP BY ?record ?recordLabel ?placeString
ORDER BY ?recordLabel
