# find manuscript records about subject "X"
SELECT ?record ?recordLabel ?subjectString ?subject ?subjectLabel
WHERE {
# bind query variables
  BIND(p:P8 AS ?subjectAsRecordedStatement).
  BIND(ps:P8 AS ?subjectAsRecorded).
  BIND(pq:P22 AS ?hasSubject).
  BIND(wdt:P49 AS ?hasExternalURI).
# statement: manuscript record has statement for subject
  ?record ?subjectAsRecordedStatement ?subjectStatement .
# statement: subject statement has subject object recorded as string value
  ?subjectStatement ?subjectAsRecorded ?subjectString .
# statement: subject statement has qualifier for structured/authority value
  ?subjectStatement ?hasSubject ?subject .
# statement: authority value is represented in FAST as URI for "Astronomy, Arab" 
  ?subject ?hasExternalURI <http://id.worldcat.org/fast/819764>.
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  }
