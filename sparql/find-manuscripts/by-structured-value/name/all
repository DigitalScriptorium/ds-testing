# find manuscript records with associated names
SELECT DISTINCT ?record ?recordLabel ?nameString ?roleLabel ?name ?nameLabel ?QID ?nameTypeLabel
WHERE {
# bind query variables
  BIND(p:P17 AS ?nameAsRecordedStatement).
  BIND(ps:P17 As ?nameAsRecorded).
  BIND(pq:P31 AS ?hasName).
  BIND(pq:P34 AS ?hasRole).
  BIND(wdt:P42 AS ?instanceOf).
  BIND(wdt:P48 AS ?hasQID).
# statement: manuscript record has statement for name
  ?record ?nameAsRecordedStatement ?nameStatement .
# statement: name statement has name object recorded as string value
  ?nameStatement ?nameAsRecorded ?nameString .
# statement: string statement has qualifier for possible role value 
  ?nameStatement ?hasRole ?role .
# statement: string statement has qualifier for possible structured/authority value, which may have QID & name type
  OPTIONAL { { ?nameStatement ?hasName ?name . } OPTIONAL { ?name ?hasQID ?QID .} OPTIONAL {?name ?instanceOf ?nameType .} }

  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  }
ORDER by ?recordLabel ?nameLabel
