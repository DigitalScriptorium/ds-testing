# find manuscript records and associated holding institutions
SELECT ?record ?recordLabel ?institutionString ?institution ?institutionLabel ?QID
WHERE {
# bind query variables
  BIND(p:P47 AS ?holdingInstitutionAsRecordedStatement).
  BIND(ps:P47 AS ?holdingInstitutionAsRecorded).
  BIND(pq:P35 AS ?hasHoldingInstitution).
  BIND(wdt:P48 AS ?hasQID).
# statement: manuscript record has statement for holding institution
  ?record ?holdingInstitutionAsRecordedStatement ?institutionStatement .
# statement: institution statement has institution object as string value
  ?institutionStatement ?holdingInstitutionAsRecorded ?institutionString .
# statement: institution statement has qualifier for structured/authority value
  ?institutionStatement ?hasHoldingInstitution ?institution .
# statement: authority value has QID
  ?institution ?hasQID ?QID.
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  }
ORDER by ?recordLabel ?institutionLabel
