# find manuscript records with genre "X"
SELECT ?record ?recordLabel ?genreString ?genre ?genreLabel
WHERE {
# bind query variables
  BIND(p:P6 AS ?genreAsRecordedStatement).
  BIND(ps:P6 AS ?genreAsRecorded).
  BIND(pq:P21 AS ?hasGenre).
  BIND(wdt:P49 AS ?hasExternalURI).
# statement: manuscript record has statement for genre
  ?record ?genreAsRecordedStatement ?genreStatement .
# statement: genre statement has genre object recorded as string value
  ?genreStatement ?genreAsRecorded ?genreString .
# statement: genre statement has qualifier for structured/authority value
  ?genreStatement ?hasGenre ?genre .
# statement: authority value is represented by URI in controlled vocabulary for "Qur'ans"
  ?genre ?hasExternalURI <http://vocab.getty.edu/aat/300265128>.
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  }
