# find names associated with all manuscripts
SELECT ?ms ?msLabel ?record ?recordLabel ?name ?nameLabel ?QID
WHERE {
# bind query variables
  BIND(p:P17 AS ?nameAsRecorded).
  BIND(pq:P31 AS ?hasName).
  BIND(wdt:P48 AS ?hasQID).
# statement: DSID is assigned to (P1) manuscript
  ?DSID ^wdt:P1 ?ms .
# statement: manuscript is described in (P7) manuscript record 
  ?ms ^wdt:P7 ?record .
# statement: manuscript record has name recorded as literal string value
  ?record ?nameAsRecorded ?nameString .
# statement: string value has qualifier name authority value
  ?nameString ?hasName ?name .
# statement: authority value has linked QID
  ?name ?hasQID ?QID .
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  }
ORDER by ?nameLabel
