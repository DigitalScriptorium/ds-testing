# find names associated with all manuscripts
SELECT ?ms ?DSID ?record ?recordLabel ?nameString ?roleLabel ?name ?nameLabel ?QID
WHERE {
# bind query variables
  BIND(wdt:P1 AS ?hasDSID).
  BIND(wdt:P7 AS ?describes).
  BIND(p:P17 AS ?nameAsRecordedStatement).
  BIND(ps:P17 AS ?nameAsRecorded).
  BIND(pq:P31 AS ?hasName).
  BIND(pq:P34 AS ?hasRole).
  BIND(wdt:P48 AS ?hasQID).
# statement: manuscript record describes manuscript
  ?record ?describes ?ms .
# statement: DSID is assigned to manuscript
  ?ms ?hasDSID ?DSID .
# statement: manuscript record has statement for name recorded as literal string value
  ?record ?nameAsRecordedStatement ?nameStatement .
# statement: name statement has name object recorded as string value
  ?nameStatement ?nameAsRecorded ?nameString .
# statement: name statement has qualifier for role
  ?nameStatement ?hasRole ?role .
# statement: name statement has qualifier for possible structured/authority value, which may have QID
  OPTIONAL { { ?nameStatement ?hasName ?name .} OPTIONAL { ?name ?hasQID ?QID .}}
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  }
ORDER by ?recordLabel ?nameLabel
