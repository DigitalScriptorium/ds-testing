# prefixes
PREFIX wd: <https://catalog.digital-scriptorium.org/entity/>
PREFIX wds: <https://catalog.digital-scriptorium.org/entity/statement/>
PREFIX wdv: <https://catalog.digital-scriptorium.org/value/>
PREFIX wdt: <https://catalog.digital-scriptorium.org/prop/direct/>
PREFIX p: <https://catalog.digital-scriptorium.org/prop/>
PREFIX ps: <https://catalog.digital-scriptorium.org/prop/statement/>
PREFIX pq: <https://catalog.digital-scriptorium.org/prop/qualifier/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# find manuscript records for manuscripts associated with particular name(s) (personal or corporate entity or entities)
SELECT ?record ?recordLabel ?nameString (GROUP_CONCAT(?nameLabel;separator="|") as ?names) ?typeLabel ?roleLabel
WHERE {
# set variable value for name or names to be queried
VALUES ?name { wd:Q383 wd:Q425 wd:Q1488 wd:Q3203 wd:Q28158 } # Lawrence Schoenberg, Catholic Church, Masters of the Gold Scrolls, Ali Qushji, Ananda Coomaraswamy
# bind query variables
  BIND(p:P14 AS ?nameAsRecordedStatement).
  BIND(ps:P14 AS ?nameAsRecorded).
  BIND(pq:P17 AS ?hasName).
  BIND(pq:P15 AS ?hasRole).
  BIND(wdt:P16 AS ?hasType).
  
# statement: manuscript record has statement for associated name
  ?record ?nameAsRecordedStatement ?nameStatement .
# statement: name statement has name object recorded as string value
  ?nameStatement ?nameAsRecorded ?nameString .
# statement: name statement has qualifier for structured/authority value
  ?nameStatement ?hasName ?name .
# statement: associated name has role
  ?nameStatement ?hasRole ?role .
# statement: authority value has type (Personal or Corporate)
  ?name ?hasType ?type .

# return English language labels
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en".
    ?record rdfs:label ?recordLabel .
    ?name rdfs:label ?nameLabel .
    ?role rdfs:label ?roleLabel .
    ?type rdfs:label ?typeLabel .
  }
  }
GROUP BY ?record ?recordLabel ?nameString ?roleLabel ?typeLabel
ORDER BY ?recordLabel
